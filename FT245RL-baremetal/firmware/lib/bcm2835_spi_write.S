/**
 * @file bcm2835_spi_write.S
 *
 */
/* Copyright (C) 2014 by Arjan van Vught <pm @ http://www.raspberrypi.org/forum/>
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:

 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.

 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

#include "bcm2835.h"
#include "bcm2835_spi.h"

.macro FUNC name
.text
.code 32
.global \name
\name:
.endm

.cpu arm1176jzf-s
.fpu vfp

FUNC bcm2835_spi_write
@ void bcm2835_spi_write(uint16_t data)
ldr	r3, =BCM2835_SPI0_BASE
ldr	r1, [r3]
orr	r1, r1, #BCM2835_SPI0_CS_CLEAR
str	r1, [r3]
ldr	r1, [r3]
orr	r1, r1, #BCM2835_SPI0_CS_TA
str	r1, [r3]
cs_txd_loop:
    ldr	r1, [r3]
    tst	r1, #BCM2835_SPI0_CS_TXD
    beq	cs_txd_loop
lsr	r1, r0, #8
uxtb	r0, r0
str	r1, [r3, #BCM2835_SPI0_FIFO]
str	r0, [r3, #BCM2835_SPI0_FIFO]
cs_done:
 	ldr	r1, [r3]
 	tst	r1, #BCM2835_SPI0_CS_DONE
 	beq	cs_done
ldr	r1, [r3]
bic	r1, r1, #BCM2835_SPI0_CS_TA
str	r1, [r3]
bx lr
